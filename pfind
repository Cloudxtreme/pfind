#!/usr/bin/perl -w
# (c) 2012 Sergey Redin sergey@redin.info https://github.com/spiculator/pfind
use strict;
use Data::Dumper;
use Parse::Yapp;
use POSIX qw/uname sysconf _SC_PAGESIZE/;

my $state = {};
my $vars = {};
my $usercmd_args = {};
my $usercmd_blocks = {};
# a copy of previous expression's result
# for things like -children/-parents/-descendants/-ancestors:
my $previous_list;
our $after_OR = undef;
my $action_triggered = undef;
# $NOPID is a pseudo-PID used when no real PID required/allowed.
# Also we cache global variables in $state->{$NOPID}
my $NOPID = -1;

my @preload = qw#
	-cmd -writeln %str [ -write %str\n ]
	-cmd -writeln1 %STR [ -write1 %STR\n ]

	-cmd -warn %WARN_MESSAGE [ -warnf %WARN_MESSAGE\n ]
	-cmd -abort [ -exit 1 ]
	-cmd -die %DIE_MESSAGE [ -warn %DIE_MESSAGE -comma -abort ]
	-if %UNAME/SYSNAME != Linux -then -die pfind\sis\sonly\sfor\slinux -fi

	-cmd -kill [ -signal KILL ]
	-cmd -term [ -signal TERM ]
	-cmd -hup  [ -signal HUP  ]

	-cmd -ps [ -exec ps uf {} ]
	-cmd -pso %PS_FIELDS [ -exec ps -o\- %PS_FIELDS {} ]

	-cmd -exe %exe_arg [ %exe == %exe_arg ]
	-cmd -cwd %cwd_arg [ %cwd == %cwd_arg ]

	%vsz := %statm/size
	%rss := %statm/resident

	-cmd -kthread [ 0 == %rss ]
	-cmd -userspace [ ! -kthread ]

	%ppid := %stat/ppid
	%comm := %stat/comm
	%s := %stat/state

	%name := %status/Name

	-cmd -grep %GREP_ARG [ %exe -m *%GREP_ARG*
		-or %comm -m *%GREP_ARG* -or %name -m *%GREP_ARG* ]
	-cmd -egrep %EGREP_ARG [ %exe =~ %EGREP_ARG
		-or %comm =~ %EGREP_ARG -or %name =~ %EGREP_ARG ]

	%command := %tree%name
	-cmd -pstree [ -settree -echo %ppid %pid %s %command ]
#;

sub get_full_proc_list() {
	return $state->{"all"} if exists $state->{"all"};
	my $list = [];
	opendir PROC, "/proc" or die $!;
	while( readdir PROC ) {
		push @$list, $_ if /^\d+$/;
	}
	closedir PROC or die $!;
	$list = [ sort { $a <=> $b } @$list ];
	return ( $state->{"all"} = $list );
}

sub check_list($) {
	my $list = shift;
	if( not defined $list ) {
		die "list not defined";
	} elsif( not ref $list ) {
		$list eq "all" or die;
		return get_full_proc_list;
	} elsif( "ARRAY" eq ref $list ) {
		return $list;
	} else {
		die;
	}
}

sub check_defined_scalar($) {
	1 == @_ or die;
	my $value = shift;
	return if defined($value) and not ref($value);
	my ($package, $filename, $line) = caller;
	die sprintf "check_defined_scalar failed at %s line %s: %s\n",
		$filename, $line, Dumper($value);
}

sub read_file($) {
	my $path = shift;
	check_defined_scalar $path;
	die "absolute path expected: $path\n" unless $path =~ /^\//;
	open FILE, "<", $path or return undef;
	my @contents = <FILE>;
	close FILE or return undef;
	return @contents;
}

sub read_line_from_file($) {
	my $path = shift;
	check_defined_scalar $path;
	die "absolute path expected: $path\n" unless $path =~ /^\//;
	open FILE, "<", $path or return undef;
	my $line = <FILE>;
	return undef unless defined $line;
	close FILE or return undef;
	chomp $line;
	return $line;
}

sub cached_get($$;@);

sub get_stat($) {
	my $pid = shift;
	my $line = join "", read_file "/proc/$pid/stat"; # comm may be multi-line
	return undef unless defined $line;
	chomp $line;
	$line =~ /^(\d+) \((.*)\) (\w) (\d+(\s+-?\d+)+)$/s or die "bad stat: $line";
	my %out = ( pid => $1, comm => $2, state => $3 );
	my @rest = split /\s+/, $4;
	my @rest_names = qw(
		ppid pgrp session tty_nr tpgid flags
		minflt cminflt majflt cmajflt
		utime stime cutime cstime
		priority nice num_threads itrealvalue starttime
		vsize rss rsslim startcode endcode startstack
		kstkesp kstkeip signal blocked sigignore sigcatch
		wchan nswap cnswap exit_signal processor
		rt_priority policy delayacct_blkio_ticks guest_time cguest_time
	);
	while( @rest ) {
		my $value = shift @rest;
		last unless defined $value;
		my $name = shift @rest_names;
		$out{$name} = $value;
	}
	return \%out;
}

sub parse_statmlike_file($@) {
	my ($path, @names) = @_;
	die unless @names;
	my $out = {};
	my $line = read_line_from_file $path;
	return $out unless defined $line;
	$line =~ /^\d[\d\s]+$/ or die "bad line: $line";
	my @values = split /\s+/, $line;
	die "wrong number of values from $path\n" unless @values == @names;
	$out->{$_} = shift @values foreach @names;
	return $out;
}

sub get_statm($) {
	my $pid = shift;
	my $out = parse_statmlike_file( "/proc/$pid/statm",
		qw/size resident share text lib data dt/ );
	my $PAGESIZE = cached_get( $NOPID, "PAGESIZE" );
	$out->{$_} *= $PAGESIZE foreach keys %$out;
	return $out;
}

my %multipliers = (
	B	=> 1,
	kB	=> 1024,
	KB	=> 1024,
	MB	=> 1024**2,
	GB	=> 1024**3,
	TB	=> 1024**4,
	PB	=> 1024**5,
	kiB	=> 1000,
	KiB	=> 1000,
	MiB	=> 1000**2,
	GiB	=> 1000**3,
	TiB	=> 1000**4,
	PiB	=> 1000**5,
);
sub fix_kb($) {
	my $value = shift;
	if( $value =~ /^(\d+)\s+(\w\w?B)$/ ) {
		my $multiplier = $multipliers{$2};
		die $value unless defined $multiplier;
		return $1 * $multiplier;
	} else {
		return $value;
	}
}

sub get_status($) {
	my $pid = shift;
	my $out = {};
	foreach ( read_file "/proc/$pid/status" ) {
		chomp;
		/^(\w+):\s+(.*)$/ or die "Bad line from status for $pid: $_\n";
		$out->{$1} = fix_kb $2;
	}
	return $out;
}

sub get_UNAME($) {
	die unless $NOPID eq shift;
	my @values = uname;
	5 == @values or die "uname() must retun 5 values\n";
	my $out = {};
	$out->{$_} = shift @values
		foreach qw/SYSNAME NODENAME RELEASE VERSION MACHINE/;
	return $out;
}

sub get_UPTIME($) {
	die unless $NOPID eq shift;
	my $line = read_line_from_file "/proc/uptime";
	$line =~ /^((\d+)(\.\d+)?)\s+((\d+)(\.\d+)?)\s*$/
		or die "bad line from /proc/uptime: $line\n";
	return $2; # the integer part of uptime
}

sub get_HZ($) {
	die unless $NOPID eq shift;
	my $release = cached_get( $NOPID, "UNAME", "RELEASE" );
	my $filename = "/boot/config-$release";
	my $gzname = "/proc/config.gz";
	if( -f $filename ) {
		open CONFIG, "<", $filename or die $!;
	} elsif( -f $gzname ) {
		open CONFIG, "zcat $gzname |" or die $!;
	} else {
		die "cannot get %HZ\n";
	}
	my $HZ = undef;
	while(<CONFIG>) {
		if( /^CONFIG_HZ=(\d+)\s*$/ ) {
			$HZ = $1;
			last;
		}
	}
	close CONFIG; # no checks
	die "cannot find CONFIG_HZ in kernel config\n" unless defined $HZ;
	return $HZ;
}

my %getters0 = (
	"pid"	=> sub { return shift },
	"exe"	=> sub { readlink("/proc/$_[0]/exe") // "" },
	"cwd"	=> sub { readlink("/proc/$_[0]/cwd") // "" },
	"root"	=> sub { readlink("/proc/$_[0]/root") // "" },
	"tree"	=> sub { "" }, # see -settree
	"UPTIME"	=> \&get_UPTIME,
	"PAGESIZE"	=> sub { $NOPID eq $_[0] ? sysconf(_SC_PAGESIZE) : die },
	"HZ"		=> \&get_HZ,
);
my %getters1 = (
	"stat"	=> \&get_stat,
	"statm"	=> \&get_statm,
	"status" => \&get_status,
	"UNAME"	=> \&get_UNAME,
	"ENV"	=> sub { $NOPID eq $_[0] ?  { %ENV } : die },
);

sub cached_get($$;@) {
	my ($pid, $name, @args) = @_;
	die "bad variable name $name: must be all upper or all lower case\n"
		if $name =~ /[A-Z]/ and $name =~ /[a-z]/;
	die "no PID allowed for global vars\n"
		if $name =~ /^[A-Z]/ and $pid ne $NOPID;
	die "bad PID: $pid for $name\n"
		if $name =~ /^[a-z]/ and not ( $pid =~ /^\d+$/ and $pid > 0 );
	if( exists $getters0{$name} ) {
		0 == @args or die "command $name takes no arguments\n";
		$state->{$pid}{$name} //= $getters0{$name}->($pid);
		return $state->{$pid}{$name} // "(undef)";
	} elsif( exists $getters1{$name} ) {
		1 == @args or die "command $name takes one argument\n";
		my ($arg1) = @args;
		$state->{$pid}{$name} //= $getters1{$name}->($pid);
		return $state->{$pid}{$name}{$arg1} // "(undef)";
	} else {
		die "unknown variable: $name\n";
	}
}

my %pattern2re_cache;
sub pattern2re($) {
	my $pattern = shift;
	my $re = $pattern2re_cache{$pattern};
	unless( $re ) {
		$re = $pattern;
		$re =~ s/([\\^.$|\(\)\[\]\{\}\+])/\\$1/g;
		$re =~ tr/?/./;
		$re =~ s/\*/.*/g;
		$re = "^$re\$";
		$re = qr/$re/;
		$pattern2re_cache{$pattern} = $re;
	}
	return $re;
}

sub cmd_list($) {
	my $list = shift;
	$action_triggered = 1;
	print "@$list\n" if @$list;
	return $list;
}

my %sprintf_escapes = (
	# \q is for -quote, \x is for hex, \0 is for oct
	a	=> "\a",
	b	=> "\b",
	e	=> "\e",
	f	=> "\f",
	n	=> "\n",
	r	=> "\r",
	s	=> " ",
	t	=> "\t",
	"\\"	=> "\\",
	"-"	=> "", # empty string, works as a separator
);

sub process_arg($;$) {
	my ($arg, $list) = @_;
	# we return hashref (if defined $list) or scalar (else)
	die $list unless not defined $list or 'ARRAY' eq ref $list;
	check_defined_scalar $arg;
	if( $arg =~ /^\\q(.*)$/ ) {
		# see also process_arg() for \q in variable values
		return( (defined $list) ? { map { $_ => $1 } @$list } : $1 );
	}
	my @outlist = ( defined($list) ? @$list : ($NOPID) );
	my $out = { map { $_ => "" } @outlist };
	my @rest = ( $arg );
	while( @rest ) {
		if( not length $rest[0] ) {
			shift @rest;
		} elsif( $rest[0] =~ /^([^\\%]+)(.*)$/ ) {
			$out->{$_} .= $1 for @outlist;
			$rest[0] = $2;
		} elsif( $rest[0] =~ /^\\x([01-9a-f]{2})(.*)$/ ) {
			my $add = chr hex $1;
			$out->{$_} .= $add for @outlist;
			$rest[0] = $2;
		} elsif( $rest[0] =~ /^\\0([0-7]*)(.*)$/ ) {
			my $num = oct $1;
			die "too big oct: \\0$1\n" if $num > 255;
			my $add = chr $num;
			$out->{$_} .= $add for @outlist;
			$rest[0] = $2;
		} elsif( $rest[0] =~ /^\\(.)(.*)$/ ) {
			my $add = $sprintf_escapes{$1} // "\\$1";
			$out->{$_} .= $add for @outlist;
			$rest[0] = $2;
		} elsif( $rest[0] =~ /^%(([a-zA-Z]\w*)(\/[a-zA-Z]\w*)*)(.*)$/ ) {
			my ($exp, $name, $args) = ($1, $2, $3);
			$rest[0] = $4;
			my $var_is_local = ( $name =~ /^[a-z]/ );
			die "you cannot use local variables here: $arg\n"
				if $var_is_local and not defined $list;
			if( exists $vars->{$name} ) {
				die "\%$name takes no args\n"
					if defined $args and length $args;
				my $value = $vars->{$name};
				check_defined_scalar $value;
				if( $value =~ /^\\q(.*)$/ ) {
					$out->{$_} .= $1 for @outlist;
				} else {
					unshift @rest, $vars->{$name};
				}
			} else {
				my ($name, @args) = split /\//, $exp;
				foreach my $pid ( @outlist ) {
					$out->{$pid} .= cached_get(
						( $var_is_local ? $pid : $NOPID),
						$name, @args
					);
				}
			}
		} else {
			die "bad string: $rest[0]\n"
		}
	}
	$out = $out->{$NOPID} if not defined $list;
	return $out;
}

sub print_table($$$@) {
	my ($list, $name, $with_header, @strings) = @_;
	my $once = not defined $list;
	$once or 'ARRAY' eq ref($list) or die;
	die "-$name expects at least one arg\n" unless @strings;

	my $columns = [];
	my $first_alignment;
	foreach my $str ( @strings ) {
		my $header;
		my $alignment = "/auto";
		if( $str =~ /((\w*)(\/\w+)?):(.+)$/ and length $1 ) {
			$str = $4;
			$header = $2 if length $2;
			$alignment = $3 ? $3 : "/auto";
		}
		if( $with_header and not defined $header ) {
			$header = ($str =~ /^%(\w[\w\/]*)$/) ? uc $1 : "";
		}
		my @values;
		if( $once ) {
			my $s = process_arg $str;
			@values = ( $s );
		} else {
			my $hashed_s = process_arg($str, $list);
			@values = map { $hashed_s->{$_} } @$list;
		}
		if( $alignment eq "/center" ) {
			s/^\s*(.*[^\s])\s*$/$1/ foreach @values;
		}
		my $maxlength = 0;
		my $allnumeric = 1;
		foreach ( @values ) {
			$allnumeric = 0 if $allnumeric and not /^-?\d+$/;
			my $l = length;
			$maxlength = $l if $l > $maxlength;
		}
		$allnumeric = 0 unless @values;
		if( $with_header ) {
			my $l = length $header;
			$maxlength = $l if $l > $maxlength;
		}
		$alignment = ( $allnumeric ? "/right" : "/left" ) if $alignment eq "/auto";
		$first_alignment = $alignment if not defined $first_alignment;
		my @column = @values;
		unshift @column, $header if $with_header;
		foreach my $i ( 0 .. $#column ) {
			my $need = $maxlength - length $column[$i];
			die if $need < 0;
			if( $alignment eq "/left" ) {
				$column[$i] .= " " x $need;
			} elsif( $alignment eq "/right" ) {
				$column[$i] = (" " x $need) . $column[$i];
			} elsif( $alignment eq "/center" ) {
				my $left = int( $need / 2 );
				my $right = $need - $left;
				$column[$i] = (" " x $left) . $column[$i] . (" " x $right);
			} else {
				die "unknown alignment: $alignment\n";
			}
		}
		push @$columns, \@column;
	}
	my $prefix = ( ($first_alignment eq "/left") ? "" : " " );
	my $table = "";
	while( @{$columns->[0]} ) {
		my @fields;
		foreach my $c ( @$columns ) {
			push @fields, shift @$c;
		}
		$table .= sprintf "%s%s\n", $prefix, join( "  ", @fields );
	}
	print $table;
	$action_triggered = 1;
}

sub do_write($$) {
	my ($list, $string) = @_;
	$action_triggered = 1;
	my $out = process_arg( $string, $list );
	print( $list ? join "", map { $out->{$_} } @$list : $out );
}

sub cmd_warnf($$) {
	my ($list, $string) = @_;
	print STDERR process_arg $string;
	return $list;
}

sub cmd_exit($$) {
	my ($list, $exitcode) = @_;
	$exitcode = process_arg $exitcode;
	$exitcode =~ /^\d+$/ or die "-exit argument must be numeric\n";
	exit $exitcode;
}

sub cmd_signal($$) {
	my ($list, $signal) = @_;
	$action_triggered = 1;
	$signal = process_arg $signal;
	$signal = uc $signal;
	return [ grep { kill $signal, $_ } @$list ];
}

sub get_additions($$$$) {
	my ($checklist, $name, $inc, $once) = @_;
	$after_OR or die "-$name must come after -or\n";
	my @out = ();
	my %previous = map { $_ => 1 } @$previous_list;
	my %left2check = map { $_ => 1 } @$checklist;
	delete $left2check{$_} foreach keys %previous;
	while( %previous and %left2check ) {
		# $inc MUST NOT change its arguments
		my @addition = $inc->( \%previous, \%left2check );
		last unless @addition;
		foreach my $pid ( @addition ) {
			next if $previous{$pid};
			die $pid unless $left2check{$pid};
			push @out, $pid;
			$previous{$pid} = 1;
			delete $left2check{$pid};
		}
		last if $once;
	}
	return [ @out ];
}

sub inc_children($$) {
	my ($previous, $left2check) = @_;
	my @addition;
	foreach my $pid ( keys %$left2check ) {
		my $ppid = cached_get( $pid, "stat", "ppid" );
		if( $previous->{$ppid} ) {
			push @addition, $pid;
		}
	}
	return @addition;
}
sub inc_parent($$) {
	my ($previous, $left2check) = @_;
	my @addition;
	foreach my $pid ( keys %$previous ) {
		my $ppid = cached_get( $pid, "stat", "ppid" );
		if( $left2check->{$ppid} ) {
			push @addition, $ppid;
		}
	}
	return @addition;
}

sub cmd_pids($$@) {
	my ($checklist, @pids) = @_;
	@pids = map { process_arg $_ } @pids;
	die "-pids takes at least one argument\n" unless @pids;
	/^\d+$/ or die "non-numeric pid in -pids: $_\n" for @pids;
	my %ok = map { $_ => 1 } @$checklist;
	return [ grep { $ok{$_} } @pids ];
}

sub cmd_exec($$@) {
	my ($checklist, @args) = @_;
	$action_triggered = 1;
	die "-exec takes at least two arguments\n" unless @args;
	my $braces_index;
	foreach my $i ( 0 .. $#args ) {
		if( '{}' eq $args[$i] ) {
			if( defined $braces_index ) {
				die "-exec expects no more than one '{}' argument\n";
			} else {
				$braces_index = $i;
			}
		}
	}
	if( defined $braces_index ) {
		$braces_index or die "'{}' cannot be the first argument of -exec\n";
		@$checklist or return $checklist;
		my @cmdline = (
			map { process_arg $_ } @args[0 .. $braces_index-1],
			@$checklist,
			map { process_arg $_ } @args[$braces_index+1 .. $#args],
		);
		system( @cmdline );
		return $checklist;
	} else {
		my $cmd = process_arg shift @args;
		my $ok;
		/%/ and $ok = 1 for @args; # TODO: replace this with some var_triggered
		$ok or die "-exec expects at least one '{}' or variable argument\n";
		my @hashed_args = map { process_arg($_, $checklist) } @args;
		my @out = ();
		foreach my $pid ( @$checklist ) {
			my @cmdline = ($cmd);
			push @cmdline, map { $_->{$pid} } @hashed_args;
			system( @cmdline );
			push @out, $pid unless $? >> 8;
		}
		return [ @out ];
	}
}

sub cmd_pidfile($$) {
	my ($checklist, $pidfile) = @_;
	$pidfile = process_arg $pidfile;
	my $pid = read_line_from_file $pidfile;
	return [] unless defined $pid;
	$pid =~ /^\d+$/ or return [];
	my %ok = map { $_ => 1 } @$checklist;
	$ok{$pid} or return [];
	return [ $pid ];
}

sub cmd_sort($$) {
	my ($checklist, $value) = @_;
	$value =~ /%[a-z]/ or die "-sort expects unquoted local variable argument\n";
	my $hashed_value = process_arg( $value, $checklist );
	foreach my $pid ( @$checklist ) {
		if( not defined $hashed_value->{$pid} ) {
			die "undefined value for $pid in -sort\n";
		} elsif( $hashed_value->{$pid} !~ /^-?\d+$/ ) {
			die "non-numeric value for $pid in -sort: $hashed_value->{$pid}\n";
		}
	}
	return [ sort {
		$hashed_value->{$a} <=> $hashed_value->{$b}
	} @$checklist ];
}

sub cmd_head($$) {
	my ($checklist, $num) = @_;
	$num = process_arg $num;
	die "-head expects positive numeric argument\n"
		unless $num =~ /^\d+$/ and $num > 0;
	my $total = @$checklist;
	$num = $total if $num > $total;
	return [ @$checklist[0..$num-1] ];
}
sub cmd_tail($$) {
	my ($checklist, $num) = @_;
	$num = process_arg $num;
	die "-tail expects positive numeric argument\n"
		unless $num =~ /^\d+$/ and $num > 0;
	my $total = @$checklist;
	$num = $total if $num > $total;
	return [ @$checklist[$total-$num .. $total-1] ];
}

sub do_settree($$$);
sub do_settree($$$) {
	my ($children, $pid, $prefix ) = @_;
	my @out = ();
	my @mychildren = @{$children->{$pid}};
	while( @mychildren ) {
		my $child = shift @mychildren;
		my $childprefix = $pid ? "$prefix \\_ " : $prefix;
		$state->{$child}{"tree"} = $childprefix;
		push @out, $child;
		my $nextprefix =
			(not $pid) ? $prefix
			: @mychildren ? "$prefix |  "
			: "$prefix    ";
		if( $children->{$child} ) {
			push @out, do_settree( $children, $child, $nextprefix );
		}
	}
	return @out;
}
sub cmd_settree($) {
	my $checklist = shift;
	my %pidset = map { $_ => 1 } @$checklist;
	my $children = { 0 => [] };
	foreach my $pid ( sort { $a <=> $b } @$checklist ) {
		my $ppid = cached_get( $pid, "stat", "ppid" );
		if( $pidset{$ppid} and $ppid > 1 ) {
			$children->{$ppid} ||= [];
			push @{$children->{$ppid}}, $pid;
		} else {
			push @{$children->{0}}, $pid;
		}
	}
	$state->{$_}{"tree"} = "" foreach @{$state->{"all"}};
	my @sorted = do_settree( $children, 0, "" );
	return \@sorted;
}

my %cmd0 = (
	"true"	=> sub { return $_[0] },
	"false"	=> sub { return [] },
	"list"	=> \&cmd_list,
	"settree"	=> \&cmd_settree,
	"children"	=> sub { get_additions( shift, "children", \&inc_children, 1 ) },
	"descendants"	=> sub { get_additions( shift, "descendants", \&inc_children, undef ) },
	"parents"	=> sub { get_additions( shift, "parents", \&inc_parent, 1 ) },
	"ancestors"	=> sub { get_additions( shift, "ancestors", \&inc_parent, undef ) },
);
my %cmd1 = (
	"write"		=> sub { my $list = shift; do_write($list, shift); $list },
	"write1"	=> sub { my $list = shift; do_write(undef, shift); $list },
	"warnf"		=> \&cmd_warnf,
	"exit"		=> \&cmd_exit,
	"signal"	=> \&cmd_signal,
	"pidfile"	=> \&cmd_pidfile,
	"sort"		=> \&cmd_sort,
	"head"		=> \&cmd_head,
	"tail"		=> \&cmd_tail,
);
my %cmd2 = (
	# reserved for user-defined commands
);
my %cmdm = (
	"pids"		=> \&cmd_pids,
	"exec"		=> \&cmd_exec,
	"print"		=> sub { my $list = shift; print_table( $list, "print",  0, @_); $list },
	"print1"	=> sub { my $list = shift; print_table( undef, "print1", 0, @_); $list },
	"echo"		=> sub { my $list = shift; print_table( $list, "echo",   1, @_); $list },
	"echo1"		=> sub { my $list = shift; print_table( undef, "echo1",  1, @_); $list },
);
my %compare = ( # do not firget to edit %compare_match and %compare_num!
	"-m"	=> sub { $_[0] =~ pattern2re $_[1] },
	"=@"	=> "-m",
	"-nm"	=> sub { $_[0] !~ pattern2re $_[1] },
	"!@"	=> "-nm",
	"=~"	=> sub { $_[0] =~ $_[1] },
	"!~"	=> sub { $_[0] !~ $_[1] },
	"=="	=> sub { $_[0] eq $_[1] },
	"!="	=> sub { $_[0] ne $_[1] },
	-eq	=> sub { $_[0] == $_[1] },
	-ne	=> sub { $_[0] != $_[1] },
	-lt	=> sub { $_[0] < $_[1] },
	-le	=> sub { $_[0] <= $_[1] },
	-gt	=> sub { $_[0] > $_[1] },
	-ge	=> sub { $_[0] >= $_[1] },
);
# match operations have constant second argument:
my %compare_match = map {$_ => 1 } qw/ -m =@ -nm !@ =~ !~ /;
# arguments of numeric operations are checked to be numeric:
my %compare_num = map { $_ => 1 } qw/ -eq -ne -lt -le -gt -ge /;

my %cmdkeywords = map { "-" . $_ => 1 } ( 
		keys(%cmd0), keys(%cmd1), keys(%cmd2), keys(%cmdm),
);
my %keywords = map { $_ => 1 } (
	",", qw/ -comma ( ) [ ] { } {} ; ! -not -and -a -or -o -quote -q -cmd :=
		? : -if -then -elif -elsif -elseif -else -fi /,
	keys(%compare),
	keys(%cmdkeywords),
);
my %reserved = ( '=' => 'please replace `=` with `:=` for assignment or `==` for comparison' );
$reserved{$_} = "$_ is reserved, please use -quote $_ if you need to use it as string"
	foreach '#', qw( _ + - * \ | / ~ ^ . @ % $ " ' ` );

sub assign($;$) {
	my ($name, $value) = @_;
	# we do not use process_arg(), this is probably not good
	check_defined_scalar $name;
	# undefined $value will remove variable
	die if ref $value;
	$name =~ /^%(\w+)$/ or
		die "left operand of := must be unquoted variable name\n";
	$name = $1; # drop initial '%'
	$name =~ /^[a-zA-Z]/ or die "variable name must start with a letter\n";
	# upper case names are reserved for global (not per-PID) variables
	die "bad variable name $name: must be all upper or all lower case\n"
		if $name =~ /[A-Z]/ and $name =~ /[a-z]/;
	if( $getters0{$name} or $getters1{$name} ) {
		die "cannot assign to system variable \%$name\n";
	} elsif( not defined $value ) {
		delete $vars->{$name};
	} elsif( exists $vars->{$name} ) {
		die "variable \%$name already exists\n";
	} else {
		$vars->{$name} = $value;
	}
}

sub check_numeric($$$;$) {
	my ($arg1, $arg2, $name, $pid) = @_;
	my $pid_message = ( defined $pid ? " for PID $pid" : "" );
	die "left operand '$arg1' of $name is not numeric$pid_message\n"
		unless $arg1 =~ /^[\-\+]?\d+$/;
	die "right operand '$arg2' of $name is not numeric$pid_message\n"
		unless $arg2 =~ /^[\-\+]?\d+$/;
}
sub eval_compare($$$$) {
	my ($checklist, $name, $arg1, $arg2) = @_;
	check_defined_scalar $name;
	die if $name eq ":=";
	my $code = $compare{$name};
	$code = $compare{$code} if not ref $code;
	die $name unless 'CODE' eq ref $code;
	if( not defined $checklist ) { # global -if condition
		$arg1 = process_arg $arg1;
		$arg2 = process_arg $arg2;
		check_numeric $arg1, $arg2, $name if $compare_num{$name};
		return $code->( $arg1, $arg2 );
	} elsif( $compare_match{$name} ) {
		my $hashed_arg1 = process_arg( $arg1, $checklist );
		$arg2 = process_arg $arg2;
		return [ grep {
			$code->( $hashed_arg1->{$_}, $arg2 )
		} @$checklist ];
	} else {
		my $hashed_arg1 = process_arg( $arg1, $checklist );
		my $hashed_arg2 = process_arg( $arg2, $checklist );
		if( $compare_num{$name} ) {
			check_numeric $hashed_arg1->{$_}, $hashed_arg2->{$_}, $name, $_
				foreach @$checklist;
		}
		return [ grep {
			$code->( $hashed_arg1->{$_}, $hashed_arg2->{$_} )
		} @$checklist ];
	}
}

sub mklexer(@) {
	my @cmdline = @_;
	@cmdline or die "mklexer: empty cmdline\n";
	my @tokenized = ();
	my $lexer = sub {
		if( not @cmdline ) {
			push @tokenized, "(end of cmdline)";
			return ( '', undef );
		}
		my $current = shift @cmdline;
		push @tokenized, $current;
		if( exists $reserved{$current} ) {
			die "$reserved{$current}\n";
		} elsif( $current =~ /^-q(uote)?$/ ) {
			@cmdline or die "unexpected end of cmdline after $current\n";
			my $qstr = '\q' . shift @cmdline;
			$tokenized[$#tokenized] = $qstr;
			return ( 'STRING', $qstr );
		} elsif( $keywords{$current} ) {
			if( $cmdkeywords{$current} ) {
				$current =~ /^-(\w+)$/ or die;
				return ( 'CMDNAME', $1 );
			} else {
				return ( $current, $current );
			}
		} elsif( $current =~ /^-(\w+)$/ ) {
			return ( 'CMDNAME', $1 );
		} else {
			return ( 'STRING', $current );
		}
	};
	my $error_sub = sub {
		shift @tokenized while @tokenized > 5;
		die unless @tokenized;
		die "Unexpected $tokenized[$#tokenized]: ... @tokenized <<<<<HERE\n",
	};
	return $lexer, $error_sub;
}

sub unite_trees($@) {
	my ($op, @trees) = @_;
	die $op unless $op =~ /^(COMMA|OR|AND)$/;
	die $op unless @trees;
	my @united;
	while( @trees ) {
		my $current = shift @trees;
		defined $current or die;;
		ref $current or die $current;;
		'ARRAY' eq ref $current or die ref $current;
		if( $op eq $current->[0] ) {
			my @current_args = @$current;
			shift @current_args;
			die $op unless @current_args;
			unshift @trees, @current_args;
		} else {
			push @united, $current;
		}
	}
	die unless @united;
	return [ $op, @united ];
}

my $grammar = <<'GRAMMAR';
%expect 3
%{
	sub unite($@);
	*unite = \&main::unite_trees;
%}
%%
exp:	comma_exp ;
comma_exp:
	or_exp
	| or_exp comma comma_exp	{ unite 'COMMA', $_[1], $_[3] }
	;
comma:
	','
	| '-comma'
	;
or_exp:
	and_exp
	| or_exp or and_exp	{ unite 'OR', $_[1], $_[3] }
	;
or:
	'-o'
	| '-or'
	;
and_exp:
	not_exp
	| not_exp and and_exp	{ unite 'AND', $_[1], $_[3] }
	;
and:
	/* empty */
	| '-a'
	| '-and'
	;
not_exp:
	atom
	| not not_exp		{ [ 'NOT', $_[2] ] }
	;
not:
	'-not'
	| '!'
	;
atom:
	command_exp
	| assignment
	| compare_exp
	| if_exp
	| cmd_definition
	| block
	;
command_exp:
	CMDNAME strings end_cmd			{ [ 'CMDRUN', $_[1], @{$_[2]} ] }
	| CMDNAME strings '{}' strings end_cmd	{ [ 'CMDRUN', $_[1], @{$_[2]}, '{}', @{$_[4]} ] }
	;
end_cmd:
	/* empty */
	| ';'
	;
strings:
	/* empty */		{ [] }
	| STRING strings	{ [ $_[1], @{$_[2]} ] }
	;
assignment:
	STRING ':=' assignment_value	{ [ 'ASSIGN', $_[1], $_[3] ] }
	;
assignment_value:
	STRING
	| '(' ca_expr ')'		{ $_[2] }
	| '{' ca_expr '}'		{ $_[2] }
	| '[' ca_expr ']'		{ $_[2] }
	;
compare_exp:
	STRING compare STRING	{ [ 'COMPARE', $_[2], $_[1], $_[3] ] }
	;
compare:
	'-m' | '=@' | '-nm' | '!@' | '=~' | '!~' | '==' | '!='
	| '-eq' | '-ne' | '-lt' | '-le' | '-gt' | '-ge'
	;
ca_expr:	# "ca" is for conditional assignment
	ca_options ca_default		{ [ 'CONDITIONAL', @{$_[1]}, $_[2] ] }
	;
ca_options:
	ca_option			{ [ $_[1] ] }
	| ca_options ca_option		{ [ @{$_[1]}, $_[2] ] }
	;
ca_option:
	condition '?' STRING ':'	{ ['OPTION', $_[1], $_[3]] }
	;
ca_default:
	STRING				{ ['DEFAULT', $_[1]] }
	;
if_exp:
	'-if' condition '-then' exp elsif_options maybe_else_option '-fi'
		{ [ 'IF', ['OPTION', $_[2], $_[4]], @{$_[5]}, @{$_[6]} ] }
	;
elsif_options:
	/* empty */			{ [] }
	| elsif_option elsif_options	{ [ $_[1], @{$_[2]} ] }
	;
elsif_option:
	elsif condition '-then' exp	{ ['OPTION', $_[2], $_[4]] }
	;
elsif:
	'-elif'
	| '-elsif'
	| '-elseif'
	;
maybe_else_option:
	/* empty */			{ [] }
	| '-else' exp			{ [ ['DEFAULT', $_[2]] ] }
	;
condition:
	compare_exp
	| not compare_exp		{ [ 'NOT', $_[2] ] }
	;
block:
	'(' exp ')'		{ [ 'BLOCK', $_[2] ] }
	| '{' exp '}'		{ [ 'BLOCK', $_[2] ] }
	| '[' exp ']'		{ [ 'BLOCK', $_[2] ] }
	;
cmd_definition:
	'-cmd' CMDNAME strings block	{ [ 'CMDDEF', $_[2], $_[3], $_[4] ] }
	;
%%
GRAMMAR

sub mktree(@) {
	my @cmdline = @_;
	push @cmdline, "-true" unless @cmdline;

	my $grammar_parser = new Parse::Yapp( input => $grammar );
	eval $grammar_parser->Output(
		classname	=> "grammar_pfind",
	);
	die $@ if $@;

	my $parser = new grammar_pfind;
	my ($lexer, $error_sub) = mklexer(@cmdline);
	return $parser->YYParse(
		yylex => $lexer,
		yyerror => $error_sub,
	);
}

sub evalcmd($$@) {
	my ($list, $name, @args) = @_;
	check_defined_scalar $_ foreach @args;
	if( $cmd0{$name} ) {
		die "command $name takes no args\n" if @args;
		return $cmd0{$name}->($list);
	} elsif( $cmd1{$name} ) {
		die "command $name takes 1 arg\n" unless 1 == @args;
		my ($arg1) = (@args);
		return $cmd1{$name}->($list, $arg1);
	} elsif( $cmd2{$name} ) {
		die "command $name takes 2 args\n" unless 2 == @args;
		my ($arg1, $arg2) = (@args);
		return $cmd2{$name}->($list, $arg1, $arg2);
	} elsif( $cmdm{$name} ) {
		return $cmdm{$name}->($list, @args);
	} elsif( $usercmd_args->{$name} ) {
		my $num = scalar @{$usercmd_args->{$name}};
		die "user defined command -$name takes $num arguments\n"
			unless @args == $num;
		foreach my $i ( 0 .. $num-1 ) {
			assign( $usercmd_args->{$name}[$i], $args[$i] );
		}
		my $out = evaltree( $list, $usercmd_blocks->{$name} );
		foreach my $i ( 0 .. $num-1 ) {
			assign( $usercmd_args->{$name}[$i] ); # remove var
		}
		return $out;
	} else {
		die "unknown command: $name\n";
	}
}

sub lists_substract($$) {
	my ($arg1, $arg2) = @_;
	'ARRAY' eq ref $_ or die for @_;
	my %skip = map { $_ => 1 } @$arg2;
	return [ grep { not $skip{$_} } @$arg1 ];
}

sub lists_add($$) {
	my ($arg1, $arg2) = @_;
	'ARRAY' eq ref $_ or die for @_;
	my %skip = map { $_ => 1 } @$arg1;
	my @addition = grep { not $skip{$_} } @$arg2;
	return [ @$arg1, @addition ];
}

sub evaltree($$);
sub evaltree($$) {
	my ($list_ref, $tree_ref) = @_;
	'ARRAY' eq ref $tree_ref or die;
	my $list = [ @$list_ref ]; my (@tree) = (@$tree_ref); # make copies
	my $op = shift @tree;
	if( $op eq 'BLOCK' ) {
		local $after_OR = undef; # this is why BLOCK is needed
		1 == @tree or die "$op takes one arg\n";
		return evaltree( $list, $tree[0] );
	} elsif( $op eq 'NOT' ) {
		local $after_OR = undef;
		1 == @tree or die "$op takes one arg\n";
		my $exp_result = evaltree( $list, $tree[0] );
		return lists_substract( $list, $exp_result );
	} elsif( $op eq 'COMMA' ) {
		local $after_OR = undef;
		@tree > 1 or die "$op takes at least two args\n";
		my $out;
		foreach( @tree ) {
			$out = evaltree( $list, $_ );
			$previous_list = $out;
		}
		return $out;
	} elsif( $op eq 'OR' ) {
		@tree > 1 or die "$op takes at least two args\n";
		my $list2check = [ @$list ];
		my $list_result = [];
		my $first_arg = 1;
		foreach (@tree) {
			local $after_OR = not $first_arg;
			last unless @$list2check;
			my $out = evaltree( $list2check, $_ );
			$list2check = lists_substract( $list2check, $out );
			$list_result = lists_add( $list_result, $out );
			$previous_list = $list_result;
			$first_arg = undef;
		}
		return $list_result;
	} elsif( $op eq 'AND' ) {
		local $after_OR = undef;
		@tree > 1 or die "$op takes at least two args\n";
		$list = check_list $list;
		foreach (@tree) {
			last unless @$list;
			$list = evaltree( $list, $_ );
			$previous_list = $list;
		}
		return $list;
	} elsif( $op eq 'IF' ) {
		local $after_OR = undef;
		@tree > 0 or die "$op takes at least one arg\n";
		foreach my $option ( @tree ) {
			die unless 'ARRAY' eq ref $option;
			if( $option->[0] eq 'DEFAULT' ) {
				die unless 2 == @$option;
				return evaltree( $list, $option->[1] );
			} elsif( $option->[0] eq 'OPTION' ) {
				die unless 3 == @$option;
				my( undef, $condition, $subtree ) = @$option;
				my $negative;
				if( $condition->[0] eq 'NOT' ) {
					die unless 2 == @$condition;
					$negative = 1;
					$condition = $condition->[1];
				}
				die unless 4 == @$condition;
				die unless 'COMPARE' eq $condition->[0];
				my( undef, $cmp_name, $left, $right ) = @$condition;
				if( $negative xor eval_compare( undef, $cmp_name, $left, $right ) ) {
					return evaltree( $list, $subtree );
				}
			} else {
				die;
			}
		}
		return $list;
	} elsif( $op eq 'CMDDEF' ) {
		local $after_OR = undef;
		3 == @tree or die;
		my ($name, $strings, $block) = @tree;
		$name =~ s/^-//;
		die "cannot define -$name: already defined\n"
			if exists $usercmd_blocks->{$name};
		die "cannot define -$name: it is a keyword\n"
			if exists $keywords{-$name};
		$usercmd_blocks->{$name} = $block;
		$usercmd_args->{$name} = $strings;
		return $list;
	} elsif( $op eq 'CMDRUN' ) {
		# $after_OR is not changed here
		@tree or die "no command name\n";
		my $name = shift @tree;
		$list = evalcmd( $list, $name, @tree );
		$previous_list = $list;
		return $list;
	} elsif( $op eq 'ASSIGN' ) {
		# $after_OR is not changed here
		2 == @tree or die;
		my ($left, $right) = @tree;
		assign( $left, $right );
		$previous_list = $list;
		return $list;
	} elsif( $op eq 'COMPARE' ) {
		# $after_OR is not changed here
		3 == @tree or die "$op takes three args\n";
		my ($compare_name, $left, $right) = @tree;
		$list = eval_compare( $list, $compare_name, $left, $right );
		$previous_list = $list;
		return $list;
	} elsif( $op eq 'EMPTY' ) {
		# $after_OR is not changed here
		return $list;
	} else {
		die "unknown op: $op\n";
	}
}

sub main() {
	my ($quiet, $dump_tree); # options
	my $default_cmd = "ps";
	while( @ARGV and $ARGV[0] =~ /^--/ ) {
		my $option = shift @ARGV;
		if( $option eq "--" ) {
			last;
		} elsif( $option eq "--quiet" ) {
			$quiet = 1;
		} elsif( $option eq "--dump-tree" ) {
			$dump_tree = 1;
		} elsif( $option eq "--dump-keywords" ) {
			printf "%s\n", join " ", sort keys %keywords;
			exit 0;
		} elsif( $option eq "--list" ) {
			$default_cmd = "list";
		} else {
			die "Unknown option: $option\n";
		}
	}
	push @ARGV, "-true" unless @ARGV;
	my $tree = mktree( @preload, ",", @ARGV );
	if( $dump_tree ) {
		eval q{
			use Data::Dumper::LispLike;
			print dumplisp $tree;
			exit 0;
		};
		local $Data::Dumper::Indent = 1;
		print Data::Dumper->Dump( [$tree], ["SyntaxTree"] );
		exit 0;
	}
	my $value = evaltree( get_full_proc_list(), $tree );
	evalcmd( $value, $default_cmd ) unless $quiet or $action_triggered;
	exit( @$value ? 0 : 1 );
}

main;
